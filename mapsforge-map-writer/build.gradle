dependencies {
    compile project(":mapsforge-core")
    compile group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
    compile(group: 'com.vividsolutions', name: 'jts', version: '1.13') {
        exclude(module: 'xercesImpl')
    }
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compileOnly group: 'org.openstreetmap.osmosis', name: 'osmosis-core', version: '0.44.1'
}

sourceSets.test.compileClasspath += configurations.compileOnly
sourceSets.test.runtimeClasspath += configurations.compileOnly

jar {
    from('src/main/config/') {
        filter {
            line -> line.replace('${mapfile.writer.version}', version)
        }
    }
}

task fatJar(type: Jar, dependsOn: classes) {
    classifier = 'jar-with-dependencies'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

artifacts {
    archives fatJar
}
